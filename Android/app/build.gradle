apply from: "$rootProject.projectDir/buildsystem/dependencies.gradle"
apply from: "$rootProject.projectDir/buildsystem/configurations.gradle"

apply plugin: 'com.android.application'
apply plugin: 'groovyx.android'
apply plugin: 'me.tatarka.retrolambda'

def library = ext.library;
def configuration = ext.configuration;
def testConfiguration = ext.testConfiguration;

android {
    compileSdkVersion configuration.compileSdkVersion
    buildToolsVersion configuration.buildToolsVersion

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/org.codehaus.groovy.transform.ASTTransformation'
        exclude 'LICENSE.txt'
    }

    defaultConfig {
        applicationId configuration.applicationId

        minSdkVersion configuration.minimumSdkVersion
        targetSdkVersion configuration.targetSdkVersion

        versionCode = configuration.versionMajor * 1000000 + configuration.versionMinor * 10000 + configuration.versionPatch * 100 + configuration.versionBuild
        versionName = "${configuration.versionMajor}.${configuration.versionMinor}.${configuration.versionPatch}"

        testInstrumentationRunner testConfiguration.testRunner
    }

    // Flavors
    productFlavors {
        dev {
            minSdkVersion configuration.targetSdkVersion
        }

        production {
            minSdkVersion configuration.minimumSdkVersion
        }
    }

    // Types
    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            resValue "string", "app_name", "DJHunter (debug)"

            debuggable true;
            zipAlignEnabled false;
            signingConfig android.signingConfigs.debug;
        }

        release {
            resValue "string", "app_name", "DJHunter"

            minifyEnabled true
            shrinkResources true
        }
    }
}

dependencies {

    // Support
    compile "${library.support_compat}"
    compile "${library.support_design}"
    compile "${library.stream}"

    // Rx
    compile "${library.rx_java}"

    // Network
    compile "${library.jsoup}"
    compile "${library.ok_http}"

    // Testing
    androidTestCompile "${library.spock_android}"
    androidTestCompile "${library.testing_support}"
    androidTestCompile "${library.spock}", {
        exclude group: 'org.codehaus.groovy'
        exclude group: 'junit'
    }
    androidTestCompile "${library.dex_maker}"

    // Languages
    androidTestCompile "${library.groovy}"
}
